# ------------------------
# One-compartment model with first-order absorption rate and 
# linear elimination (metabolism or other route of elimination) 
#
# version 1
#
# Units: 
# - time in hours
# - volumes in liters
# - masses of substances in micromoles
# - concentrations of substances in microM
# ------------------------

States  = {Q_rest,
           Q_elim,
}; 

Outputs = {Oral_input_uM,
           C_rest,          # Concentration in central compartment (uM)
           C_elim,          # Total metabolize concentration (uM)
           Q_total};        # Total quantity for mass balance (mg)

Inputs  = {Oral_input};     # Drug input in micromoles


# ------------------------
# Parameters
# ------------------------

MW; # molecular weight (g/mole)

# Oral input modeling

Absdose;
IngDose    = 1.0; # ingested input (mg)
Fgutabs    = 1.0; #
Period     = 0.0; # period of the exposure/no exposure cycle (h)
Tlag       = 0.0; # Absorption lagtime (h)
Ka         = 0.0; # Intestinal absortion rate (/h)
Oral_input = PerExp (Absdose, Period, Tlag, Ka);

# Volumes (L)
V_dist  = 1; 

# Transfer rate constants between compartments (/h)
k_elim = 1;

Initialize{
    Absdose = IngDose * Fgutabs;
}

Dynamics {
  dt (Q_rest)  = Oral_input - k_elim * Q_rest;
  dt (Q_elim)  = k_elim * Q_rest; 
}

CalcOutputs {
  Oral_input_uM = Oral_input * 1000 / MW;
  C_rest  = Q_rest  / V_dist / MW * 1000;
  C_elim  = Q_elim * 1000 / MW;
  Q_total = Q_rest + Q_elim;
}

End.
